#pragma kernel GetGrassData

#include "Simplex.compute"

struct GrassData 
{
    float3 position;
    float2 uv;
    float displacement;
};

RWStructuredBuffer<GrassData> grassDataBuffer;

uint grassFieldResolution, grassDensity;

Texture2D<float4> _HeightMap;
SamplerState sampler_HeightMap;

float _DisplacementStrength;

[numthreads(8,8,1)]
void GetGrassData (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= grassFieldResolution || id.y >= grassFieldResolution) return;

    GrassData grassData;

    // calculate positions
    float3 grassPos = 0.0f;
    grassPos.xz = id.xy - (float)grassFieldResolution * 0.5f + 0.5f;
    grassPos.xz *= (1.0f / float(grassDensity));

    // offset position with noise
    grassPos.x += snoise(float3(grassPos.xz, 0.0f) * 3.0f) * 0.732f;
    grassPos.z += snoise(float3(grassPos.xz, 0.0f) * 4.0f) * 0.648f;

    // calculate uv
    float2 uv = id.xy / float(grassFieldResolution);

    // offset height with noise
    float3 noise = _HeightMap.SampleLevel(sampler_HeightMap, uv, 0).rgb;
    float luminance = dot(noise, float3(0.2126, 0.7152, 0.0722));
    float normalizedDisplacement = luminance;
    float displacement = normalizedDisplacement * _DisplacementStrength;
    grassPos.y += displacement;

    // finalizing buffer
    grassData.position = grassPos;
    grassData.uv = uv;
    grassData.displacement = displacement;

    uint index = id.y * grassFieldResolution + id.x;
    grassDataBuffer[index] = grassData;
}
